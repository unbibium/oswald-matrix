;
; Screen Headliner was originally a program from Compute!'s 
; Gazette, which allows you to draw PETSCII characters at four 
; times the regular size, using the PETSCII quadrant characters.
;
; This version has the following improvements:
;  * More compact so that the PETSCII quadrant table can be
;    stored contiguously -- the original version required
;    an extra READ loop.
;  * Controls the cursor position and RVS state by manipulating
;    the OS variables instead of outputting control codes.
;  * Can print against the right edge of the screen without
;    breaking up the character.
;  * Can pass parameters through SYS command.
;    warning, there are no bounds checks
;        SYS 50000,10,"HI" will print "HI" at x=10
; 

    processor 6502

.CHROUT        = $FFD2

; zero-page variables
ROWLOOP  = $02
FONTPAGE = $04
FONTADDR = $03
TOPROW   = $05
BOTROW   = $06

; array to store the 16 petscii characters to write
PIXELS   = $0230

; PETSCII constants
CR         = $0D
; OS locations for manipulating output
RVS        = $C7
COLUMN     = $D3

; parameters
; starting X-position of character
XPOS       = $F9
; screen code of character to draw
CHAR       = $FA
; this is reused as the index in the final loop

; default origin.

CHKCOM = $AEFD
FRMEVL = $AD9E
TXTPTR = $99
FRESTR = $B6A3
STRING = $69
TEMPY = STRING+2

HLBASIC:
        echo "HLBASIC = SYS", [.]d, ",X,A$"
        JSR CHKCOM
        JSR GETBYT
        STX XPOS
        JSR CHKCOM
        JSR FRMEVL
        JSR FRESTR
        ; todo: get integer
        stx STRING
        sty STRING+1
        STA TEMPY
       
.HLLOOP:
        ldy #0
        LDA (STRING),y
        ; convert petscii to screen code
        cmp #33
        bcc .SKIPPRINT
        cmp #64
        bcc .ANUMBER
        eor #$40
.ANUMBER
        sta CHAR
        jsr HEADLINER
        dec TEMPY
        beq .HLBEND
        ; move cursor up and increment xpos
        ldx #4
uploop:
        inc XPOS
        lda #$91  ;CURSOR-UP
        
        jsr .CHROUT
        dex
        bne uploop
.INCS:  ; increment string ptr
        INC STRING
        BNE .HLLOOP
        INC STRING+1
        BNE .HLLOOP
.SKIPPRINT ; move to next character without crsr-up
        lda XPOS
        clc
        adc #4
        sta XPOS
        dec TEMPY
        bne .INCS
.HLBEND:
        lda #0
        sta XPOS
        RTS

; original screen headliner routine
HEADLINER:
	echo "HEADLINER = SYS", [.]d
            LDA $D018  ; get displayed font from VIC
            AND #$02
            LSR
            ADC #$D0 >> 3
            LDX #$03
.hc363:
            ASL CHAR
            ROL
            DEX
            BNE .hc363
            STA FONTPAGE
            LDA CHAR
            STA FONTADDR
            LDA $DC0E
            AND #$FE
            STA $DC0E
            LDA $01
            AND #$FB  ; set $D000 to character set ROM
            STA $01
            LDA #$00
            STA CHAR
            LDA #$05
            STA ROWLOOP
.hc388:
            LDY #$00
            LDA (FONTADDR),Y
            STA TOPROW
            INC FONTADDR
            LDA (FONTADDR),Y
            STA BOTROW
            INC FONTADDR
            DEC ROWLOOP
            BEQ .hc3b6
            LDX #$04
.hc39c:
            LDA #$00
            ASL BOTROW
            ROL
            ASL BOTROW
            ROL
            ASL TOPROW
            ROL
            ASL TOPROW
            ROL
            LDY CHAR
            STA PIXELS,Y
            INC CHAR
            DEX
            BNE .hc39c
            BEQ .hc388
.hc3b6:
            LDA $01
            ORA #$04 ; map I/O to $D000
            STA $01
            LDA $DC0E
            ORA #$01 ; timer underflow on port B bit 6?
            STA $DC0E
            LDY #$00
.MOVERIGHT:
            LDX XPOS
            STX COLUMN
            LDA #$04
            STA BOTROW
.hc3d6:
            LDA PIXELS,Y
            TAX
            LDA QUADRANTS,X
            AND #$40
            STA RVS
            LDA QUADRANTS,X
            AND #$BF
            JSR .CHROUT
            INY
            DEC BOTROW
            BNE .hc3d6
            LDA XPOS
            CMP #36  ; printing at right edge of screen?
            BMI DOCR
	    ; add and subtract 40 to X position.
	    EOR #104
	    STA XPOS
	    BNE NOCR
DOCR
            LDA #CR
            JSR .CHROUT
NOCR
            CPY #$10
            BNE .MOVERIGHT
          RTS

;
; PETSCII characters representing the pixel graphic
; characters.  Bit $40 is a flag for whether to turn
; reverse on for this character, and is filtered out.
;
QUADRANTS
        byte 32,188,190,226,172,225,191,251
        byte 187,255,161,236,162,254,252,96


